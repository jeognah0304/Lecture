/* CLUSTER1.SAS : CLUSTER ANALYSIS OF FLYING MILEAGES BETWEEN CITIES */
OPTIONS PS=60;
DATA  MILEAGES(TYPE=DISTANCE);   
  INPUT  (ATLANTA CHICAGO DENVER HOUSTON LOSANGEL
        MIAMI NEWYORK SANFRAN SEATTLE WASHDC) (5.)
        @55 CITY $15.;
  CARDS;
   0                                                  ATLANTA
 587    0                                             CHICAGO
1212  920    0                                        DENVER
 701  940  879    0                                   HOUSTON
1936 1745  831 1374    0                              LOS ANGELES
 604 1188 1726  968 2339    0                         MIAMI
 748  713 1631 1420 2451 1092    0                    NEW YORK
2139 1858  949 1645  347 2594 2571    0               SAN FRANCISCO
2182 1737 1021 1891  959 2734 2408  678    0          SEATTLE
 543  597 1494 1220 2300  923  205 2442 2329    0     WASHINGTON D.C.
;
RUN;
PROC CLUSTER DATA=MILEAGES  METHOD=SINGLE;   
  ID CITY;
RUN;
PROC TREE;         
RUN;
PROC CLUSTER DATA=MILEAGES  METHOD=AVERAGE;   
  ID CITY;
RUN;
PROC TREE;         
RUN; QUIT;



/* CLUSTER2.SAS : ANALYSIS OF FISHER'S IRIS DATA */
DATA IRIS;
  INFILE 'Z:\D_drive\개인용무\2014_2학기_시립대학교강의\miae_lecture\실습\20141127\IRIS.txt';
   INPUT X1-X4 SPEC_NO @@;
   IF SPEC_NO=1 THEN SPECIES='SETOSA    ';
   IF SPEC_NO=2 THEN SPECIES='VERSICOLOR';
   IF SPEC_NO=3 THEN SPECIES='VIRGINICA ';
   LABEL X1='SEPAL LENGTH'
         X2='SEPAL WIDTH '
         X3='PETAL LENGTH'
         X4='PETAL WIDTH ';
RUN;
PROC CLUSTER DATA=IRIS METHOD=WARD PRINT=15 CCC;    
  VAR X1-X4;
  COPY SPECIES;                   
RUN;
PROC PLOT;                        
  PLOT _CCC_*_NCL_ / VPOS=26
    HAXIS=1 TO 25 BY 1;
RUN;
PROC TREE NOPRINT NCL=3 OUT=OUT;  
  COPY X1-X4 SPECIES;
RUN;
PROC FREQ;                        
  TABLES CLUSTER*SPECIES;
RUN;
PROC CANDISC NOPRINT OUT=CAN;    
  CLASS CLUSTER;
  VAR X1-X4;
RUN;
PROC PLOT;
  PLOT CAN2*CAN1=CLUSTER;         
RUN; QUIT;



/* CLUSTER3.SAS : ANALYSIS OF BODY SIZE DATA */
OPTIONS PS=40;
DATA PHYSICAL;
  INFILE 'Z:\D_drive\개인용무\2014_2학기_시립대학교강의\miae_lecture\실습\20141127\PHYSICAL.txt' FIRSTOBS=2 MISSOVER;
  INPUT ID V1-V10;
  LABEL V1='신장' V2='총길이' V3='등길이' V4='화장' V5='소매길이'
        V6='바지길이' V7='밑위앞뒤' V8='가슴둘레' V9='허리둘레'
        V10='엉덩이둘레';
RUN;
PROC CLUSTER DATA=PHYSICAL METHOD=WARD PRINT=0 CCC;
  VAR V1 V2 V5 V6 V8 V9 V10;
RUN;
PROC PLOT;
  PLOT _CCC_*_NCL_/HAXIS=1 TO 20 BY 1;
RUN; QUIT;


/* CLUSTER4.SAS : ANALYSIS OF BODY SIZE DATA  */
DATA PHYSICAL;
   INFILE 'Z:\D_drive\개인용무\2014_2학기_시립대학교강의\miae_lecture\실습\20141127\PHYSICAL.txt' FIRSTOBS=2 MISSOVER;
  INPUT ID V1-V10;  
  LABEL V1='신장' V2='총길이' V3='등길이' V4='화장' V5='소매길이'
        V6='바지길이' V7='밑위앞뒤' V8='가슴둘레' V9='허리둘레'
       V10='엉덩이둘레';
RUN;
PROC FASTCLUS DATA=PHYSICAL MAXCLUSTERS=10 OUT=OUTCLUS; 
  VAR V1 V2 V5 V6 V8 V9 V10;
RUN;
PROC SORT DATA=OUTCLUS;           
  BY CLUSTER;
RUN;
PROC MEANS DATA=OUTCLUS NOPRINT;
  VAR V1 V2 V5 V6 V8 V9 V10;
  BY CLUSTER;
  OUTPUT OUT=CLMEAN MEAN=MEANV1 MEANV2 MEANV5 MEANV6 MEANV8-MEANV10; 
RUN;
PROC SORT DATA=CLMEAN OUT=CLMEAN;   
  BY MEANV1;
RUN;
PROC PRINT DATA=CLMEAN;            
  VAR MEANV1 MEANV2 MEANV5 MEANV6 MEANV8-MEANV10 _FREQ_;
RUN; QUIT;


